10 CLEAR 1500
15 KEY OFF
20 ON ERROR GOTO 31900
30 CLS
89 GOSUB 90:GOTO 100
90 PRINT@7,"IMPRES - (c)HTI     v.2.02":RETURN
100 GOSUB 110
105 J$=INKEY$:GOTO 105
110 KEY OFF:CALL 17034:GOSUB 31200
120 KEY 1,"STRT":KEY 2,"VI":KEY 3,"SUMP":KEY 4,"PROP":KEY 5,"VEH":KEY 6,"END":KEY 7,"":KEY 8,"EXIT"
130 ONKEY GOSUB 200,300,400,500,600,700,,31000:KEY ON:CALL 17064:RETURN
200 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"EVNT":KEY 2,"SUPP":KEY 8,"EXIT":ON KEY GOSUB 1000,2000,,,,,,110:CALL 17064:KEY ON:RETURN
300 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"VI":KEY 2,"PHY":KEY 3,"INJ":KEY 8,"EXIT":ON KEY GOSUB 3000,4000,5000,,,,,110:CALL 17064:KEY ON:RETURN
400 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"SUMP":KEY 2,"PHY":KEY 3,"INJ":KEY 4,"ARR":KEY 8,"EXIT":ON KEY GOSUB 6000,4000,5000,8000,,,,110:CALL 17064:KEY ON:RETURN
500 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"PROP":KEY 2,"DOC":KEY 8,"EXIT":ON KEY GOSUB 9000,12000,,,,,,110:CALL 17064:KEY ON:RETURN
600 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"VEH":KEY 8,"EXIT":ON KEY GOSUB 11000,,,,,,,110:CALL 17064:KEY ON:RETURN
700 KEY OFF:CALL 17034:GOSUB 31200:KEY 1,"END":KEY 8,"EXIT":ON KEY GOSUB 7000,,,,,,,110:CALL 17064:KEY ON:RETURN
900 RESTORE 900:RETURN:DATA 7,N,N/A,W,WHT,B,BLK,I,AMR/IND,O,ORIEN,H,HISPANIC,U,UNK
910 RESTORE 910:RETURN:DATA 2,1,ARST,2,NTA
912 RESTORE 912:RETURN:DATA 3,A,ADLT,J,JV,N,N/A
960 RESTORE 960:RETURN:DATA 3,A,ATMP,C,COMMIT,N,N/A
962 RESTORE 962:RETURN: DATA 11, A,AMPHET,B,BARBIT,C,COCAINE,E,HEROIN,H,HALLUC,M,MARIJ,N,N/A,O,OPIUM/DIRIV,P,PARAPH,S,SYNTH,U,UNK
963 RESTORE 963:RETURN:DATA 4,0,N/A,1,YES,2,NO,8,UNK
965 RESTORE 990:RETURN
966 RESTORE 966:RETURN:DATA 16,00,N/A,01,HANDGUN,02,RIFLE,03,SHOTGUN,04,FIREARM,05,CUT INST.,06,BLUNT OBJ,07,HAND/ETC,08,POIS,09,EXPLO,10,FIRE/INCEND,11,THREAT,12,SIMUL,13,DRG,88,UNK,99,OTH,
969 RESTORE 969:RETURN:DATA 6, V,VICTIM,W,WITNESS,R,REPORTEE,O,OWNER,B,WI/REPORTEE,Z,OTH
971 RESTORE 971:RETURN:DATA 3,0,N/A,1,RES,2,NON-RES
972 RESTORE972:RETURN:DATA 4,0,NONE,1,MINOR,2,SERIOUS,3,FATAL
973  RESTORE 973:RETURN:DATA 11,00,N/A,01,SHOT,02,STAB,03,CUT,04,UNCON,05,BRK BONE,06,INTERNAL,07,BRK TEETH,08,BURN,09,ABRAS,99,OTH
974 RESTORE 993:RETURN
975 RESTORE 975:RETURN:DATA 5, S,SUSP,A,ARREST,E,ESCAPE,M,MISSING,R,REC MP
976 RESTORE 976:RETURN:DATA 9,0,N/A,1,RUNAWAY,2,PARENTAL,3,INVOL,4,DISAB,5,ENDANGE,6,DISAST,7,VOL ADLT,8,UNK
977 RESTORE 977:RETURN:DATA 9,0,N/A,1,VOL,2,LOCAT,3,HOSP,4,DBS,5,LE CUS,6,RET PARN,7,DEAD,9,OTH
978 RESTORE 978:RETURN:DATA 9, 1,STL,2,REC,3,STL/REC,4,REC OTH JUR,5,LOST,6,FOUND,7,SAFEKEEP,8,EVID,9,OTH
979 RESTORE 995:RETURN
980 RESTORE 980:RETURN:DATA 6,0,N/A,1,ARSON,2,MDOP,3,DUR OTH,4,STRIP,9,OTH
981 RESTORE 981:RETURN:DATA 9, 1,STL,2,REC,3,REPO,4,SUSP,5,IMPOUND,6,ABAND,7,FAIL RETU,8,SEIZ,9,EVID
982 RESTORE982:RETURN:DATA10,1,CAR,2,TRK/VAN,3,CYCLE,4,CAMPER/REC,5,BUS,6,TRAILER,7,BOAT,8,AIRCRAFT,9,OTH,0,SNMOBL
983 RESTORE 983:RETURN:DATA 10,0,N/A,1,RES,2,APART,3,PROJ,4,COMER/INDUS,5,PARK/PLAY,6,MALL,7,WOODS,8,WATER,9,OTH
984 RESTORE 984:RETURN:DATA 4,0,N/A,1,STL/REC LOC,2,STL LOC/REC OTH,3,STL OTH/REC LOC
985 RESTORE 985:RETURN:DATA 9,1,FORGE,2,UTTER,3,COUNTERFT,4,INSUF FUND,5,ACC CLS,6,USE FRAUD,7,FORGE/UTTER,8,COUNTER/UTTER,9,OTH
986 RESTORE 986:RETURN:DATA 8,1,CHECK,2,CRED CRD,3,MONEY ORD,4,BOND/CERT,5,ATM CARD,6,IDEN,7,PRESC,9,OTH
987 RESTORE 987:RETURN:DATA 4,0,N/A,1,ARST,2,EXP CLR,3,UNFND
988 RESTORE 988:RETURN:DATA 7,0,N/A,1,NO EXTR,2,ARST PRIME,3,DEATH,4,VI/WI REFU,5,NO PROS,6,JV
993 DATA 25,00,N/A,01,UNK02,STRANGER,03,SPOUSE,04,EX-SPOUSE,05,CO-HABIT,06,PAREN,07,SIBLING,08,CHILD,09,STEP-PAR,10,STEP-CHLD,11,IN-LAW,12,OTH FAM,13,STUDEN,14,TEACH,15,CHLD BOY/GIRL FRN
994 DATA 16,BOY/GIRL FRN,17,FRIEND,18,NEIGHB,19,SITTER,20,EMPLOYE,21,EMPLOYER,22,OWNER/TENANT,23,ACQUAINT,99,OTH
995 DATA 26,A,AUTO/PRT,B,BICY,C,CAMRA/EQP,D,DRG,E,TOOL,F,FOOD/LIQ,G,GUN,H,APPLI,I,PLANT,J,JEW/PRE MTL,K,CLOTHES,L,LIVESTOCK,M,MUS INST,N,CONST MCH,O,OFC EQP,P,ART/COL
996 DATA Q,COMPU/EQP,R,RAD/STER,S,SPORT,T,TV/VID,U,CUR/NEGO,V,CRED/CRD NONNEGO,W,BOAT MTR,X,STRUCTURE,Y,FARM EQP,Z,MISC
1000 RESTORE 1000:GOSUB 31700:RETURN:DATA INCIDENT,17
1010 DATACASE #,8,NO,0,DESC,32,*,0,BUS,20,*,0,LOC ,25,*,0,CASE CODE,5,*,0,*RP DATE,6,*,0,RP TIME,4,*,0,RP DAY,3,*,0,*FR DATE,6,*,0
1020 DATA FR TIME,4,*,0,FR DAY,3,*,0,*TO DATE,6,*,0,TO TIME,4,*,0,TO DAY,3,*,0,SEC,2,*,0,AREA,1,*,0,UCR CD,5,*,0
2000 RESTORE 2000:GOSUB 31700:RETURN:DATA SUPPLEMENT,3
2010 DATA CASE #,8,NO,0,DESC,25,*,0,DATE,6,*,0
3000 RESTORE 3000:GOSUB 31700:RETURN:DATA VI/WI,20
3010 DATA VI/WI CD,1,*,10,VI/WI#,2,*,0,RACE,1,RACE/SEX,31,SEX,1,0,0,DOB,6,*,0,RES STAT,1,*,12,REL OFN,2,*,15,NAME,32,*,0,ADRS,25,*,0,CTY,15,*,0,ST,2,*,0,ZIP,10,*,0,H-PH,14,*,0,OCC,15,*,0
3020 DATA EMP,32,*,0,ADRS,25,*,0,CTY,15,*,0,ST,2,*,0,ZIP,10,*,0,B-PH,14,*,0
4000 RESTORE 4000:GOSUB 31700:RETURN:DATA PHYSICAL,18
4010 DATA DESC OF,4,*,0,RACE,1,RACE/SEX,31,SEX,1,0,0,AGE,5,*,0,HGT,7,*,0,WGT,7,*,0,HAIR LGH,7,*,0,HAIR STY,7,*,0,COL HAIR,3,*,0,COL EYE,3,*,0,BLD,5,*,0,COMPLEX,5,*,0,FACE HAIR,10,*,0,GLASSES ?,1,*,0,SCAR TATOO,20,*,0,SCAR TATOO,20,*,0
4020 DATA DRESS,31,*,0,DRESS,31,*,0
5000 RESTORE 5000:GOSUB 31700:RETURN:DATA INJ/ATTACK,12
5010 DATA TYP ATCK,20,*,0,HOW ATCK,28,*,0,WEP,2,*,7,NAT/INJ,2,*,14,LOC BDY,15,*,0,1ST AID BY,20,*,0,ADMIT ?,1,*,0,HOSP,10,*,0,COND,1,*,13,M.E. CALL ?,1,*,0,M.E.,10,*,0,DISP BDY,20,*,0
6000 RESTORE 6000:GOSUB 31700:RETURN:DATA SU/MP,29
6010 DATA SU/MP CD,1,*,16,SU/MP#,2,*,0,RACE,1,RACE/SEX,31,SEX,1,0,0,AGE,2,*,0,DOB,6,*,0,POB,20,*,0,NAME,32,*,0,AKA,32,*,0,ADRS,25,*,0,CTY,15,*,0,WT,2,*,0,ZIP,10,*,0,H-PH,14,*,0,DL,15,*,0,DL/ST,2,*,0,OCC,25,*,0,EMP,32,*,0
6020 DATA ADRS,25,*,0,CTY,15,*,0,ST,2,*,0,ZIP,10,*,0,RES STAT,1,*,12,B-PH,14,*,0,SS#,9,*,0,MP TYP,1,*,17,FOUL PLY,1,*,4,MP MISS PREV,1,*,0,REC INFO,1,*,18,
7000 RESTORE 7000:GOSUB 31700:RETURN:DATA ENDING,5
7010 DATA OFCR,20,*,0,ID#,3,*,0,CLR TYP,1,*,28,DT CASE CLR,6,*,0,ADT JV,1,*,33
8000 RESTORE 8000:GOSUB 31700:RETURN:DATA ARREST,21
8010 DATA ARREST OF,4,*,0,AR#,2,*,0,AR TYP,1,*,32,AR DT,6,*,0,AR TIME,4,*,0,WEP SEIZ TYP,2,*,7,INF ALC,1,*,0,INF DRG,1,*,0,DRG TYP,1,*,3,JACKET#,8,*,0
8020 DATA*CHARGE,25,*,0,CNTS,2,*,0,*CHARGE,25,*,0,CNTS,2,*,0,*CHARGE,25,*,0,CNTS,2,*,0,F/PRINT,15,*,0,PHOTO BY,15,*,0,OFFICER,20,*,0,OFFICER,20,*,0,A/JV ?,1,*,0
9000 RESTORE 9000:GOSUB 31700:RETURN:DATA PROPERTY,12
9010 DATA PROP STAT,1,*,19,TYP,1,*,20,DMG CD,1,*,21,VAL STL/DMG,8,*,0,VAL REC,8,*,0,DT REC,6,*,0,DESC,25,*,0,BRAND,15,*,0,MOD,15,*,0,CAL,5,*,0,SER#,20,*,0,COL,7,*,0
11000 RESTORE 11000:GOSUB 31700:RETURN:DATA VEH/VES,24
11010 DATA VH/VS STAT,1,*,22,TYP,1,*,23,VAL STL/DMG,9,*,0,DT REC,6,*,0,VAL REC,9,*,0,LOC REC,1,*,24,VEH DMG CD,1,*,21,YR,2,*,0,MAKE,10,*,0,MOD,10,*,0,STY,10,*,0,LGH,10,*,0,COL,7,*,0,LIC YR,2,*,0
11020 DATA *LIC/ST,2,*,0,LIC#,8,*,0,VIN/SER#,25,*,0,COND,9,*,0,LOCK ?,1,*,0,KEY ?,1,*,0,TOW BY,20,*,0,HOLD ?,1,*,0,LEIN HOLD,20,*,0,TB#,5,*,0
12000 RESTORE 12000:GOSUB 31700:RETURN:DATA DOCUMENT,18
12010 DATA DOC CD,1,*,26,TYP CD,1,*,27,AMT,8,*,0,BNK DRAWN ON,15,*,0,ADRS,25,*,0,CTY,15,*,0,ST,2,*,0,ZIP,10,*,0,DT ACCP,6,*,0,TM ACCP,4,*,0,DT DOC,6,*,0
12011 DATA DOC#,7,*,0,ACC#,16,*,0,ACCP BY,20,*,0,ENDOR,20,*,0,PAY TO,20,*,0,SIGNED,20,*,0,ID USED,25,*,0
20000 CLS:PRINT CHR$(27);"V":IF TN<5 THEN DT=TN ELSE DT=5
20010 FOR X=1 TO TN:B$(X)=STRING$(LL(X)," "):NEXT
20020 PRINT@1,TT$:PRINT@20,"FILE NAME: ";RPT$:LINE (0,7)-(239,7),1:FOR X=1 TO DT:INIT=(X*40):PRINT@INIT,TITLIB$(X);:P=INIT+LEN(TITLE$(X))+2:PRINT@P,"[";B$(X);"]":NEXT
20030 X=1:RP=40:SCN=0:CNT=1
20035 GOSUB 22000
20040 FSP=RP+LEN(TITLE$(X))+1+CNT+1
20050 I$=INKEY$:IF CS=1 THEN PRINT@FSP,"_";CHR$(29);:CS=0 ELSE PRINT@FSP, MID$(B$(X),CNT,1);CHR$(29);:CS=1
20055 IF LC(X)<>0 THEN IF CS=1 THEN PRINT@255,""ELSE PRINT@255,"    " ELSE PRINT@255,"    "
20060 IF I$="" THEN 20050
20070 I=ASC(I$):IF I=29 THEN IF CNT>1 THEN CNT=CNT-1:PRINT@FSP,MID$(B$(X),CNT+1,1):GOTO 20040 ELSE 20040
20080 IF I=28 THEN IF CNT<LEN(B$(X)) THEN CNT=CNT+1:PRINT@FSP,MID$(B$(X),CNT-1,1):GOTO 20040 ELSE 20040
20090 IF I=8 THEN IF CNT>1 THEN MID$(B$(X),CNT-1,1)=" ":PRINT@FSP,MID$(B$(X),CNT,1):PRINT@FSP-1," ";:CNT=CNT-1:GOTO 20040 ELSE GOTO 20040
20100 IF I=13 OR I=31 THEN PRINT@FSP,MID$(B$(X),CNT,1);:CNT=LEN(B$(X)):GOTO 20160
20110 IF I=30 AND RP>40 THEN PRINT@FSP,MID$(B$(X),CNT,1);:RP=RP-40:X=X-1:CNT=1:GOTO 20040
20120 IF I=30 AND SCN>0 THEN SCN=SCN-1:RP=120:CNT=1:X=X-1:GOSUB 31550:GOTO 20040
20130 IF I=30 AND SCN=0 AND RP=40 THEN GOTO 20040
20150 MID$(B$(X),CNT,1)=I$:PRINT@FSP,I$;
20155 IF LC(X)<>0 AND CNT=LL(X) THEN GOSUB 27000
20160 IF CNT=LEN(B$(X)) THEN IF X<TN THEN X=X+1:CNT=1:IF RP=200 THEN RP=120:SCN=SCN+1:CNT=1:GOSUB 31500:GOTO 20040 ELSE RP=RP+40:GOTO 20040 ELSE GOTO 20040
20170 CNT=CNT+1:GOTO 20040
22000 GOSUB 31200:KEY 1,"ABRT":KEY 8,"SAVE":KEY 4,"CODE":ON KEY GOSUB 25000,,,23000,,,,24000:KEY ON:CALL 17064:RETURN
23000 REM
23001 CLS:KEY OFF:CALL 17034
23010 IF LC(X)=0 THEN PRINT@130,"NO LIBRARY":FOR Z=1 TO 500:NEXT:GOTO 23500
23020 ZZ=0:GOSUB 30105:FOR Z=1 TO LP:GOSUB 30135
23035 IF Z<=LP THEN RR=(ZZ*40)+10:PRINT@RR,CD$;:ZZ=ZZ+1
23040 IF ZZ=5 THEN PRINT@245,"PRESS ENTER FOR MORE - ";:LINE INPUT YY$:IF YY$="Q" OR YY$="q" THEN 23500 ELSE CLS:ZZ=0
23050 NEXT:PRINT@245,"PRESS ENTER TO CONTINUE - ";:LINE INPUT YY$
23500 CLS:OO=X:X=(X-(RP/40))+3:GOSUB 31505:X=OO:PRINT@1,TT$:PRINT@20,"FILE NAME: ";RPT$:LINE (0,7)-(239,7),1:CALL 17064:KEY ON:RETURN
24000 CLS:PRINT@130,"SAVING"
24010 CLOSE:OPEN RPT$ FOR APPEND AS 1
24020 IF TT$="INCIDENT" THEN 24022 ELSE 24025
24022 PRINT#1,"XXXXXX ZZZZZZZZ POLICE DEPARTMENT ":PRINT#1,"CASE REPORT                               CASE # : ";B$(1):PRINT#1,"ORI#: 5074000":PRINT#1,STRING$(79,"=")
24025 IF TT$="SUPPLEMENT" THEN PRINT#1,"SUPPLEMENT                                   CASE # : ";B$(1):PRINT#1,"ORI#: 5074000":PRINT#1,STRING$(79,"=")
24030 IF TT$="ENDING" THEN PRINT#1,"":PRINT#1,STRING$(79,"=")
24040 IF TT$<>"ENDING" AND TT$<>"INCIDENT" AND TT$<>"SUPPLEMENT" THEN PRINT#1,"___[ ";TT$;" ]";:PL=80-(LEN(TT$)+8):PRINT#1,STRING$(PL,"_")
24050 FOR X=1 TO TN:IF LC(X)=0 OR LEN(B$(X))=LL(X) THEN B$(X)="["+B$(X)+"]" ELSE FF=(LEN(B$(X))-(LL(X))):PP$="["+MID$(B$(X),1,LL(X))+MID$(B$(X),LL(X)+1,FF)+"]":B$(X)=PP$
24052 IF LC(X)<>0 AND LEN(B$(X))=LL(X)+2 THEN B$(X)=B$(X)+"[INVALID]"
24055 NEXT
24060 ST=0:CNT=0:FOR X=1 TO TN:FD$=""
24061 IF PTITLE$(X)="NO" THEN GOTO 24090
24062 IF CNT<>0 AND CNT+3>ST THEN ST=ST+5:GOTO 24062 ELSE FD$=STRING$(ST-CNT," ")
24065 IF PTITLE$(X)="" THEN FD$="":ST=ST-5
24067 IF MID$(PTITLE$(X),1,1)="*" THEN PTITLE$(X)=MID$(PTITLE$(X),2,LEN(PTITLE$(X))-1):PRINT#1,"":ST=0:CNT=0:FD$=""
24070 IF CNT+LEN(B$(X))+LEN(FD$)+LEN(PTITLE$(X))>75 THEN PRINT#1,"":FD$="":ST=0:CNT=0
24080 PRINT#1,FD$;PTITLE$(X);" ";B$(X);:CNT=CNT+LEN(PTITLE$(X))+LEN(B$(X))+LEN(FD$)
24085 ST=ST+5
24090 NEXT
24100 PRINT#1,"":IF TT$="ENDING" THEN PRINT#1,STRING$(79,"=")
24105 IF TT$="INCIDENT" OR TT$="SUPPLEMENT" THEN PRINT#1,STRING$(79,"_")
24110 PRINT#1,""
24120 REM
24130 REM
24900 RUN
25000 CLS:KEY OFF:GOSUB 31200:GOTO 24120
27000 P$=MID$(B$(X),1,LL(X)):B$(X)=P$:GOSUB 30105:FOR M=1 TO LP:GOSUB 30135
27010 OO=LEN(B$(X)):IF MID$(CD$,1,OO)=B$(X) THEN QQ=LEN(CD$)-(OO+3):B$(X)=B$(X)+"]["+MID$(CD$,OO+4,QQ):GOTO 27025
27020 NEXT:B$(X)=B$(X)+"][INVALID"
27025 PRINT@FSP-LL(X),"[";B$(X);"]";:OP=40-(LEN(TITLIB$(X))+LEN(B$(X))+5):PRINT STRING$(OP," ");
27026 CNT=LEN(B$(X))
27030 RETURN
30000 CALL 17034:CLS:FILES:PRINT@280,"ENTER FILE NAME: ";:LINE INPUT RPT$:IF RPT$="" THEN RETURN
30020 IF LEN(RPT$)>6 THEN RETURN ELSE RPE=ASC(MID$(RPT$,1,1)):IF RPE<65 OR RPE>90 THEN IF RPE<97 OR RPE>122 THEN RETURN
30035 CLS:PRINT@130,"LOADING"
30040 READ TT$,TN:DIM LL(TN),LC(TN),TITLE$(TN),PTITLE$(TN),B$(TN):FOR X=1 TO TN:READ TITLE$(X),LL(X),PTITLE$(X),LC(X)
30050 IF PTITLE$(X)="*" THEN PTITLE$(X)=TITLE$(X) ELSE IF PTITLE$(X)="0" THEN PTITLE$(X)=""
30055 IF MID$(TITLE$(X),1,1)="*" THEN TITLE$(X)=MID$(TITLE$(X),2,LEN(TITLE$(X))-1)
30060 NEXT
30100 GOTO 20000
30105 REM
30110 IF LC(X)=0 THEN 30200
30120 ON LC(X) GOSUB 960,,962,963,,965,966,,,969,,971,972,973,974,975,976,977,978,979,980,981,982,983,984,985,986,987,988,,900,910,912,,,
30130 READ LP:RETURN
30135 READ CD$:READ KK$:CD$=CD$+" - "+KK$
30200 RETURN
31000 KEY OFF:GOSUB 31200:KEY 8,"MENU"+CHR$(13):MENU
31200 KEY OFF:FOR F=1 TO 8:KEY F,"":NEXT:RETURN
31400 CLS:GOSUB 31200:GOSUB 90:GOSUB 110:RETURN
31500 FOR M=1 TO 6:CL=(M*40):PRINT@CL,STRING$(40," ");:NEXT
31505 IF X+2>TN THEN EC=TN ELSE EC=X+2
31510 Z=1:FOR Y=X-2 TO EC:INIT=(Z*40):PRINT@INIT,TITLE$(Y);:P=LEN(TITLE$(Y))+INIT+2:PRINT@P,"[";B$(Y);"]";:Z=Z+1:NEXT:RETURN
31550 FOR M=1 TO 5:CL=(M*40):PRINT@CL,STRING$(40," ");:NEXT
31560 Z=1:FOR Y=X-2 TO X+2:INIT=Z*40:PRINT@INIT,TITLE$(Y);:P=INIT+LEN(TITLE$(Y))+2:PRINT@P,"[";B$(Y);"]";:Z=Z+1:NEXT:RETURN
31700 KEY OFF:GOSUB 31200:GOSUB 30000:GOSUB 31400:RETURN
31900 BEEP:GOTO 31000

